
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.11.1'
    }
}

apply plugin: 'android'

android {
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    repositories {
        mavenCentral()
    }

    buildToolsVersion "19.1.0"
    compileSdkVersion 19

    defaultConfig {
        versionName '5.0.0RC3'
        versionCode 1506
	    minSdkVersion 14
        targetSdkVersion 19
    }

    productFlavors {
        free {
            applicationId 'nl.mpcjanssen.todotxtholo'
        }
        // Special flavor for testing as this is uninstalled after
        // completion of the test run
        unit {
            applicationId 'nl.mpcjanssen.simpletask.test'
        }
        cloudless {
            applicationId 'nl.mpcjanssen.simpletask'
        }
    }

    dependencies {
        freeCompile fileTree(dir: 'libs', include: '*.jar')
        compile 'com.intellij:annotations:+@jar'
        compile 'com.android.support:support-v4:19.1.0'
        compile 'org.commonjava.googlecode.markdown4j:markdown4j:2.2-cj-1.0'
        compile 'com.darwinsys:hirondelle-date4j:1.5.1'
        compile 'com.google.guava:guava:17.0'
    }

    signingConfigs {
        release {
            keyAlias = "mpcjanssen.nl"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    check.dependsOn 'connectedAndroidTestUnitDebug'
}

// Only sign if we have keystore properties in the gradle.properties in ~/.gradle
if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}
