buildscript {
    ext.kotlin_version = '1.0.4'
    ext.android_support_version = '24.2.0'
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.3.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
// apply plugin: 'com.getkeepsafe.dexcount'


android {
    buildToolsVersion "24.0.1"
    compileSdkVersion 24

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }

    defaultConfig {
        versionName '8.2.3'
        versionCode 3050
	minSdkVersion 14
        targetSdkVersion 24
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    productFlavors {
        dropbox {
            applicationId 'nl.mpcjanssen.todotxtholo'
	    manifestPlaceholders = [ providerFlavour:"dropbox"]
        }
        cloudless {
            applicationId 'nl.mpcjanssen.simpletask'
	    manifestPlaceholders = [ providerFlavour:"cloudless"]
        }
    }

    signingConfigs {
        release {
            keyAlias = "mpcjanssen.nl"
        }
    }

    buildTypes {
    
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
	    manifestPlaceholders = [ provider:"nl.mpcjanssen.simpletask.debug.provider"]
        }
        release {
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
	        minifyEnabled true
            proguardFile "proguard.txt"
	    manifestPlaceholders = [ provider:"nl.mpcjanssen.simpletask.provider"]
        }
    }
    dexOptions {
        preDexLibraries = true
        javaMaxHeapSize "3g"
        dexInProcess = true
    }
}

repositories {
    jcenter()
}

dependencies {
    dropboxCompile fileTree(dir: 'libs', include: '*.jar')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.android.support:support-v4:$android_support_version"
    compile "com.android.support:design:$android_support_version"
    compile "com.android.support:appcompat-v7:$android_support_version"
    compile "com.android.support:recyclerview-v7:$android_support_version"
    compile "de.greenrobot:greendao:2.1.0"
    compile 'com.github.rjeschke:txtmark:0.11'
    compile 'com.darwinsys:hirondelle-date4j:1.5.1'
    compile 'org.luaj:luaj-jse:3.0.1'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile('com.android.support.test:runner:0.5') {
	// Necessary if your app targets Marshmallow (since the test runner
        // hasn't moved to Marshmallow yet)
        exclude group: 'com.android.support', module: 'support-annotations'    
    }
    androidTestCompile('com.android.support.test:rules:0.5') {
	// Necessary if your app targets Marshmallow (since the test runner
        // hasn't moved to Marshmallow yet)
        exclude group: 'com.android.support', module: 'support-annotations'    
    }

}

// Only sign if we have keystore properties in the gradle.properties in ~/.gradle
if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}
